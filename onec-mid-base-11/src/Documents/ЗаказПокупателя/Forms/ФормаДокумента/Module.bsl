
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//++MG 17.06.2024
	л_КонтактноеЛицо = Элементы.Вставить("КонтактноеЛицо",Тип("ПолеФормы"),Элементы.ГруппаШапкаПраво,Элементы.Договор);
    л_КонтактноеЛицо.ПутьКДанным = "Объект.КонтактноеЛицо";
	л_КонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	
	л_КонтактноеЛицо.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент");
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НоваяСвязь);
	
	
	л_КонтактноеЛицо.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);	
	
	л_ГруппаСкидка = Элементы.Добавить("ГруппаСкидка",Тип("ГруппаФормы"),Элементы.ГруппаШапкаЛево);
	л_ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	л_ГруппаСкидка.Заголовок = "";  
	л_ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	л_ГруппаСкидка.ОтображатьЗаголовок = ЛОЖЬ; 
	
	л_ГруппаСкидка.Группировка =  ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	
	л_СогласованнаяСкидка = Элементы.Добавить("СогласованнаяСкидка",Тип("ПолеФормы"),л_ГруппаСкидка);
    л_СогласованнаяСкидка.ПутьКДанным = "Объект.СогласованнаяСкидка";
	л_СогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода; 
	л_СогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ПриИзмененииСкидки");	
	
	
	КомандаРассчитатьСкидку = ЭтаФорма.Команды.Добавить("КомандаРассчитатьСкидку");
	КомандаРассчитатьСкидку.Заголовок = "Пересчитать таблицу";
	КомандаРассчитатьСкидку.Действие = "КомандаРассчитатьСкидку"; 	
	
	л_РассчитатьСкидку = Элементы.Добавить("РассчитатьСкидку",Тип("КнопкаФормы"),л_ГруппаСкидка);
	л_РассчитатьСкидку.Заголовок = "Пересчитать таблицу";  
	л_РассчитатьСкидку.Картинка = БиблиотекаКартинок.Перечитать;  
	л_РассчитатьСкидку.Отображение = ОтображениеКнопки.КартинкаИТекст;
	л_РассчитатьСкидку.ИмяКоманды = "КомандаРассчитатьСкидку";
	//--
	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	//++  MG 17.06.2024
	ТекущиеДанные.Сумма = ТекущиеДанные.Сумма - (ТекущиеДанные.Сумма *Объект.СогласованнаяСкидка)/100;
	РассчитатьСуммуДокумента();
	// -- 
	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//++MG 17.06.2024
&НаКлиенте
Процедура КомандаРассчитатьСкидку(Команда) 
	// В примере  решения при нажатии на кнопку @рассчитать скидку@ 
	// Окна с вопросом не появляется.  Только при изменении  поля "СогласованнаяСкидка"
	//  выдается вопрос.   
	Если не Объект.Товары.Количество() = 0 
		Или не Объект.Услуги.Количество() = 0  тогда
		ЗадатьВопросПользователю(); 
	КонецЕсли; 

КонецПроцедуры


&НаКлиенте
Процедура ЗадатьВопросПользователю()
    Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Изменён процент скидки. Пересчитать табличную часть документа?'"), Режим, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	ВыполнитьДействиеПриИзмененииСкидки();	

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСкидки()   
	Если не Объект.Товары.Количество() = 0 
		Или не Объект.Услуги.Количество() = 0 тогда
		ЗадатьВопросПользователю(); 
	КонецЕсли;  
	
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьДействиеПриИзмененииСкидки()   
	
	Для каждого СтрокаТовар из Объект.Товары Цикл
		РассчитатьСуммуСтроки(СтрокаТовар);
	КонецЦикла; 
	
	Для каждого СтрокаУслуга из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(СтрокаУслуга);
	КонецЦикла;

	
	РассчитатьСуммуДокумента();
КонецПроцедуры



//--


#КонецОбласти

#КонецОбласти
